apply plugin: 'com.android.application'
check.dependsOn 'assembleMinSdkJellybeanDebugAndroidTest', 'assembleMinSdkIcsDebugAndroidTest'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"
    flavorDimensions "minSdkVersion"

    defaultConfig {
        applicationId "com.google.firebase.quickstart.auth"
        minSdkVersion 17
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    productFlavors {

        // Build variant with minSdk 16 to include Facebook and FirebaseUI libraries.
        minSdkJellybean {
            dimension "minSdkVersion"
            minSdkVersion 16
        }

        // Build variant with minSdk 14, excludes Facebook and FirebaseUI libraries.
        minSdkIcs {
            dimension "minSdkVersion"
            minSdkVersion 14
        }
    }
}

configurations.all {
    resolutionStrategy.force 'com.android.support:support-annotations:26.1.0'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile 'com.android.support.test:runner:0.5'

    compile 'com.android.support:appcompat-v7:26.+'
    compile 'com.android.support:animated-vector-drawable:26.1.0'
    compile 'com.android.support:cardview-v7:26.1.0'
    compile 'com.android.support:design:26.1.0'

    compile 'com.google.firebase:firebase-auth:11.4.2'
    compile 'com.google.android.gms:play-services-auth:11.4.2'

    minSdkJellybeanCompile 'com.firebaseui:firebase-ui-auth:2.3.0'
    minSdkJellybeanCompile 'com.facebook.android:facebook-android-sdk:4.25.0'
    minSdkJellybeanCompile 'com.android.support:customtabs:26.1.0'

    compile('com.twitter.sdk.android:twitter-core:2.3.0@aar') {
        transitive = true
    }
    compile('com.twitter.sdk.android:twitter:3.1.1@aar') {
        transitive = true
    }

    testCompile 'junit:junit:4.12'
}

apply plugin: 'com.google.gms.google-services'